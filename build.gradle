plugins {
    id "java"
    id "java-library"
    id "groovy"
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.0'
}

group = "net.covers1624"
archivesBaseName = "GradleStuff"
version = "3.0.0"

targetCompatibility = '1.8'
sourceCompatibility = '1.8'

version = "$version." + (System.getenv("BUILD_NUMBER") ?: "1")
println "Starting build of ${archivesBaseName}, Version: ${version}"

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://maven.covers1624.net/" }
    maven { url "https://maven.minecraftforge.net/" }
}

dependencies {
    implementation gradleApi()

    // Required
    api 'net.covers1624:Quack:0.3.7.29'

    // Steam
    implementation 'net.platinumdigitalgroup:JVDF:1.7.2'
    implementation 'net.rubygrapefruit:native-platform:0.14'

    // XZ
    implementation 'org.tukaani:xz:1.9'

    compileOnly 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:22.0.0'
}

jar {
    from file("LICENSE.txt")
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    from file("LICENSE.txt")
}

license {
    header = file("LICENSE-header.txt")
    properties {
        year = new Date().format("yyyy")
    }
    newLine false

    include 'net/covers1624/gradlestuff/**'
}

publishing {
    repositories {
        if (System.getenv('MAVEN_PASS')) {
            maven {
                url "https://nexus.covers1624.net/repository/maven-releases/"
                credentials {
                    username 'covers1624'
                    password System.getenv('MAVEN_PASS')
                }
            }
        }
    }
    publications {
        plugin(MavenPublication) { publication ->
            groupId project.group
            artifactId project.archivesBaseName
            version project.version

            from components["java"]

            artifact sourcesJar

            pom {
                name = archivesBaseName
                description = archivesBaseName
                //The publish plugin doesnt like GString's here apparently..
                url = "https://github.com/covers1624/${archivesBaseName}".toString()
                scm {
                    url = "https://github.com/covers1624/${archivesBaseName}".toString()
                    connection = "scm:git:git://github.com/covers1624/${archivesBaseName}.git".toString()
                    connection = "scm:git:git@github.com:covers1624/${archivesBaseName}.git".toString()
                }

                issueManagement {
                    system = 'github'
                    url = "https://github.com/covers1624/${archivesBaseName}/issues".toString()
                }
                licenses {
                    license {
                        name = "MIT"
                        url = "https://raw.githubusercontent.com/covers1624/${archivesBaseName}/master/LICENSE.txt".toString()
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'covers1624'
                        name = 'covers1624'
                    }
                }
            }
        }
    }
}
